import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
	id "com.wynprice.cursemaven" version "2.1.1"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	modImplementation "svenhjol.charm:charm:${charm_version}"

	// REI
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${project.rei_version}"
	modCompile "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"

	implementation "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
	implementation "com.electronwill.night-config:core:${project.tomlnc_version}"
	implementation "com.electronwill.night-config:toml:${project.tomlnc_version}"

	include "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
	include "com.electronwill.night-config:core:${project.tomlnc_version}"
	include "com.electronwill.night-config:toml:${project.tomlnc_version}"

//	modImplementation "curse.maven:${project.terrestria_version}"
}

repositories {
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	exclude("META-INF/*.variants")

	//expand resource templates
	filesMatching("**/*.template.json") {
		JsonBuilder json = new JsonBuilder(new JsonSlurper().parseText(it.file.text))
		String template = new JsonBuilder(json.content.template).toPrettyString()
		List<String> variants = file("${sourceSets.main.resources.srcDirs[0]}/META-INF/${json.content.variants}.variants").readLines().findAll { !it.isEmpty() }
		File dir = it.relativePath.parent.getFile(file("$buildDir/resources/main"))
		dir.mkdirs()
		String name = it.name.replace(".template.json", ".json")
		String[] replacements = variants.remove(0).split(':')
		for (variant in variants) {
			Map<String, String> s = [replacements, variant.split(':', -1)].transpose().collectEntries()
			def file = new File(dir, s.inject(name) { r, e -> r.replace(e.key, e.value) })
			if (!file.exists()) {
				file.createNewFile()
				file.text = s.inject(template) { r, e -> r.replace(e.key, e.value) }
			}
		}
		it.exclude()
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

repositories {
	flatDir {
		dirs "libs"
	}
}
